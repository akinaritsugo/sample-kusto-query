// 仮想マシンにNICを結合
resources
| where type =~ "microsoft.compute/virtualmachines"
| extend vmSize = properties.hardwareProfile.vmSize
| extend powerState = tostring(split(tolower(properties.extended.instanceView.powerState.code), "powerstate/")[1])
| mvexpand nic = properties.networkProfile.networkInterfaces
| extend nicId = tolower(nic.id)
| join kind=leftouter hint.strategy=shuffle (
  data
  | where type =~ "microsoft.network/networkinterfaces"
  | extend virtualMachineName = extract('Microsoft.Compute/virtualMachines/(.*)', 1, tostring(properties.virtualMachine.id))
  | extend ipConfigCount=array_length(properties.ipConfigurations)
  | extend ipConfigurations=iff(ipConfigCount == 0, dynamic(null), properties.ipConfigurations)
  | mvexpand ipConfiguration=ipConfigurations limit 400
  | extend isPrimary = (ipConfigCount == 1 or ipConfiguration.properties.primary =~ 'true' or isempty(ipConfiguration))
  | extend publicIpAddressId = iff(isPrimary, tolower(ipConfiguration.properties.publicIPAddress.id), '')
  | extend privateIpAddress = iff(isPrimary, ipConfiguration.properties.privateIPAddress, '-')
  | join kind=leftouter hint.strategy=shuffle (
      data
        | where type =~ 'microsoft.network/publicipaddresses'
        | extend publicIpAddress = tostring(properties.ipAddress)
        | project publicIpAddressId=tolower(id), publicIpAddress
      ) on publicIpAddressId
  | project nicId = tolower(id), name, privateIpAddress, publicIpAddress
) on nicId
